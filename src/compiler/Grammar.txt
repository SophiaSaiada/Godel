@Tokens
    WhiteSpace, SemiColon, BreakLine, Colon, Dot, Comma, Apostrophes,
    Percentage, Backslash, Star, Minus, Plus, Division, ExclamationMark, QuestionMark, Ampersand, SingleOr,
    Keyword, Assignment, QuestionedDot, Hash,

    OpenBraces, CloseBraces, OpenParenthesis, CloseParenthesis, OpenBrokets, CloseBrokets,
    DecimalLiteral, SimpleName,

    Elvis, Or, And, Equal, NotEqual, GreaterThanEqual, LessThanEqual, NullAwareDot, RightArrow,

    KeywordVal, KeywordVar, KeywordFun, KeywordClass, KeywordTrue, KeywordFalse,
    KeywordIf, KeywordElse, KeywordWhile, KeywordPrivate, KeywordPublic, KeywordWhen, KeywordReturn,

    Unknown

@Rules
<Program> -> <SpaceStar> <Statements> <SpaceStar>
<Statements> -> <Statement> | <Statement> <WhitespacePlus> <SEMI> <Statements> | <Statement> <SEMI> <Statements> | ε

<Statement> -> <Expression> | <Declaration>

<Declaration> -> <ValDeclaration> | <ClassDeclaration> | <FunctionDeclaration>

<ValDeclaration> -> KeywordVal <SpacePlus> SimpleName <SpaceStar> <ValDeclarationRest>
<ValDeclarationRest> -> Colon <SpaceStar> <Type> <SpaceStar> Assignment <PaddedExpression> |
                        Assignment <PaddedExpression>

<Type> -> SimpleName <TypeArgumentsOptional> <QuestionMarkOptional> |
          OpenParenthesis <SpaceStar> <TypeStar> <SpaceStar> CloseParenthesis <WhitespaceStar> <FunctionalOrNullableType>
<FunctionalOrNullableType> -> RightArrow <SpaceStar> <Type> | QuestionMark | ε
<TypeStar> -> <Type> <SpaceStar> <TypeStarRest> | ε
<TypeStarRest> -> Comma <SpaceStar> <TypeStar> | ε

<TypeArgumentsOptional> -> <TypeArguments> | ε
<TypeArguments> -> OpenBrokets <SpaceStar> <TypeArgumentsContent> CloseBrokets
<TypeArgumentsContent> -> <Type> <SpaceStar> <TypeNamedArgumentsOptional> <SpaceStar> <TypeArgumentsContentRest>
<TypeNamedArgumentsOptional> -> Assignment <SpaceStar> <Type> | ε
<TypeArgumentsContentRest> -> Comma <SpaceStar> <TypeArgumentsContent> | ε

<TypeParameters> -> OpenBrokets <SpaceStar> <TypeParametersNamesPlus> <SpaceStar> CloseBrokets | ε
<TypeParametersNamesPlus> -> SimpleName <SpaceStar> <TypeParametersInheritanceOptional> <TypeParametersNamesPlusRest>
<TypeParametersInheritanceOptional> -> Colon <SpaceStar> <Type> | ε
<TypeParametersNamesPlusRest> -> Comma <SpaceStar> <TypeParametersNamesPlus> | ε
<QuestionMarkOptional> -> QuestionMark | ε

<ParenthesizedExpression> -> OpenParenthesis <SpaceStar> <Expression> <SpaceStar> CloseParenthesis
<PaddedExpression> -> <SpaceStar> <Expression> <WhitespaceStar>
<Expression> -> <IfExpression> | <ElseExpression> | <ElvisExpression>
<SimpleExpression> -> <Number> | <BooleanLiteral> | <StringLiteral> | SimpleName | <Lambda>
<BooleanLiteral> -> KeywordFalse | KeywordTrue
<Number> -> DecimalLiteral <NumberRest>
<NumberRest> -> Dot <DecimalLiteralOrMemberAccess> | ε
<DecimalLiteralOrMemberAccess> -> DecimalLiteral | <MemberAccessWithoutFirstDot>
<MemberAccessWithoutFirstDot> -> <SpaceStar> SimpleName

<StringLiteral> -> Apostrophes <AnythingEndsWithApostrophes>
<AnythingButApostrophes> ->
    WhiteSpace | SemiColon | BreakLine | Colon | Dot | Comma |
    Percentage | Backslash | Star | Minus | Plus | Division | ExclamationMark | QuestionMark | Ampersand | SingleOr |
    Assignment | QuestionedDot | Hash |

    OpenBraces | CloseBraces | OpenParenthesis | CloseParenthesis | OpenBrokets | CloseBrokets |
    DecimalLiteral | SimpleName |

    Elvis | Or | And | Equal |
    NotEqual | GreaterThanEqual |
    LessThanEqual | NullAwareDot | RightArrow

    KeywordVal | KeywordVar | KeywordFun | KeywordClass | KeywordTrue | KeywordFalse |
    KeywordIf | KeywordElse | KeywordWhile | KeywordWhen |

    Unknown
<AnythingEndsWithApostrophes> -> <AnythingButApostrophes> <AnythingEndsWithApostrophes> | Apostrophes
<IfExpression> -> KeywordIf <SpaceStar> <ParenthesizedExpression> <SpaceStar>
                  <StatementOrBlock> <WhitespaceStar> <IfExpressionRest>
<IfExpressionRest> -> <ElseExpression> | ε
<ElseExpression> -> KeywordElse <SpaceStar> <StatementOrBlock>

// Class
<ClassDeclaration> -> KeywordClass <SpacePlus> SimpleName <SpaceStar> OpenBraces
                      <SpaceStar> <MemberDeclarationStar> <SEMIOptional> CloseBraces
<MemberDeclarationStar> -> <MemberDeclaration> <WhitespaceStar> <MemberDeclarationStarRest> | ε
<MemberDeclarationStarRest> -> <SEMI> <MemberDeclarationStar> | ε

<MemberDeclaration> -> <PrivateOrPublic> <SpacePlus> <MemberDeclarationRest>
<MemberDeclarationRest> -> <ValDeclaration> | <FunctionDeclaration>

<PrivateOrPublic> -> KeywordPrivate | KeywordPublic


// Binary Operators
<ElvisExpression> -> <ReturnExpression> <WhitespaceStar> <ElvisExpressionRest>
<ElvisExpressionRest> -> Elvis <SpaceStar> <ElvisExpression> | ε

<ReturnExpression> -> KeywordReturn <SpacePlus> <ReturnExpression> | <InfixExpression>

<InfixExpression> -> <DisjunctionExpression> <WhitespaceStar> <InfixExpressionRest>
<InfixExpressionRest> -> SimpleName <SpaceStar> <InfixExpression> | ε

<DisjunctionExpression> -> <ConjunctionExpression> <WhitespaceStar> <DisjunctionExpressionRest>
<DisjunctionExpressionRest> -> Or <SpaceStar> <DisjunctionExpression> | ε

<ConjunctionExpression> -> <EqualityExpression> <WhitespaceStar> <ConjunctionExpressionRest>
<ConjunctionExpressionRest> -> And <SpaceStar> <ConjunctionExpression> | ε

<EqualityOperator> -> Equal | NotEqual
<EqualityExpression> -> <ComparisonExpression> <WhitespaceStar> <EqualityExpressionRest>
<EqualityExpressionRest> -> <EqualityOperator> <SpaceStar> <EqualityExpression> | ε

<ComparisonOperator> -> OpenBrokets | CloseBrokets | LessThanEqual | GreaterThanEqual
<ComparisonExpression> -> <AdditiveExpression> <WhitespaceStar> <ComparisonExpressionRest>
<ComparisonExpressionRest> -> <ComparisonOperator> <SpaceStar> <ComparisonExpression> | ε

<AdditiveOperator> -> Plus | Minus
<AdditiveExpression> -> <MultiplicativeExpression> <WhitespaceStar> <AdditiveExpressionRest>
<AdditiveExpressionRest> -> <AdditiveOperator> <SpaceStar> <AdditiveExpression> | ε

<MultiplicativeOperator> -> Star | Division | Percentage
<MultiplicativeExpression> -> <MemberAccess> <WhitespaceStar> <MultiplicativeExpressionRest>
<MultiplicativeExpressionRest> -> <MultiplicativeOperator> <SpaceStar> <MultiplicativeExpression> | ε

<DotOrQuestionedDot> -> Dot | NullAwareDot
<MemberAccess> -> <Invocation> <WhitespaceStar> <MemberAccessRest>
<MemberAccessRest> -> <DotOrQuestionedDot> <SpaceStar> <MemberAccess> | ε

<Invocation> -> <SimpleOrParenthesizedExpression> <WhitespaceStar> <InvocationArgumentsStar>
<InvocationArgumentsStar> -> <TypeArguments> <InvocationArguments> <WhitespaceStar> <InvocationArgumentsStar> |
                             <InvocationArguments> <WhitespaceStar> <InvocationArgumentsStar> | ε
<InvocationArguments> -> OpenParenthesis <SpaceStar> <ArgumentStar> CloseParenthesis
<ArgumentStar> -> <Expression> <SpaceStar> <NamedArgumentPostfixOptional> <ArgumentStarRest> | ε
<NamedArgumentPostfixOptional> -> Assignment <SpaceStar> <Expression> <SpaceStar> | ε
<ArgumentStarRest> -> Comma <SpaceStar> <ArgumentStar> | ε

<SimpleOrParenthesizedExpression> -> <SimpleExpression> | <ParenthesizedExpression>


// Function Declaration
<FunctionDeclaration> -> KeywordFun <SpacePlus> SimpleName <SpaceStar> <TypeParameters> <SpaceStar>
                         <FunctionParameters> <SpaceStar> <ReturnTypeOptional> <SpaceStar> <Block>
<ReturnTypeOptional> -> Colon <SpaceStar> SimpleName | ε
<FunctionParameters> -> OpenParenthesis <FunctionParameterStar> CloseParenthesis
<FunctionParameterStar> -> <FunctionParameter> <FunctionParameterStarRest> | ε
<FunctionParameterStarRest> -> Comma <FunctionParameterStar> | ε
<FunctionParameter> -> SimpleName Colon SimpleName

<StatementOrBlock> -> <Block> | <Statement>
<Block> -> OpenBraces <Program> CloseBraces

// Lambda
<Lambda> -> Hash OpenBraces <SpaceStar> <LambdaParametersStar> RightArrow <SpaceStar> <Program> CloseBraces
<LambdaParametersStar> -> SimpleName <SpaceStar> Colon <SpaceStar> <Type> <SpaceStar> <LambdaParametersRest> | ε
<LambdaParametersRest> -> Comma <SpaceStar> <LambdaParametersStar> | ε

// General Stuff
<WhiteSpaceOrBreakLine> -> WhiteSpace | BreakLine

<SpaceStar> -> <WhiteSpaceOrBreakLine> <SpaceStar> | ε
<SpacePlus> -> <WhiteSpaceOrBreakLine> <SpaceStar>

<WhitespaceStar> -> WhiteSpace <WhitespaceStar> | ε
<WhitespacePlus> -> WhiteSpace <WhitespaceStar>

<SEMI> -> <WhitespacePlus> <SEMIRest> <SpaceStar> | <SEMIRest> <SpaceStar>
<SEMIRest> -> BreakLine <SpaceStar> <SemiColonOptional> | SemiColon

<SEMIOptional> -> <SEMI> | ε

<SemiColonOptional> -> SemiColon | ε