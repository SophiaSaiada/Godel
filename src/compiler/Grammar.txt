@Tokens
    WhiteSpace, SemiColon, BreakLine, Colon, Dot, Comma, Apostrophes, Underscore,
    Percentage, Backslash, Star, Minus, Plus, ExclamationMark, QuestionMark, And, Or,
    Assignment, OpenBraces, CloseBraces, OpenParenthesis, CloseParenthesis, OpenBrokets, CloseBrokets,
    DecimalLiteral, SimpleName,

    KeywordVal, KeywordVar, KeywordFun, KeywordClass, KeywordTrue, KeywordFalse,
    KeywordIf, KeywordElse, KeywordWhile, KeywordPrivate, KeywordPublic, KeywordWhen,

    Unknown

@Rules
<Statements> -> <Statement> <WhitespaceStar> <StatementsRest> | ε
<StatementsRest> -> <SEMI> <Statements> | ε

<Statement> -> <Expression> | <Declaration>
<Block> -> OpenBraces <Statements> CloseBraces
<BlockOrStatement> -> <Block> | <Statement>

<Declaration> -> <ValDeclaration> | <ClassDeclaration>
<ValDeclaration> -> KeywordVal <SpacePlus> <UnderscoreStar> SimpleName <SpaceStar> <ValDeclarationRest>
<ValDeclarationRest> -> Colon <SpaceStar> SimpleName <SpaceStar> Assignment <PaddedExpression> |
                        Assignment <PaddedExpression>

<ParenthesizedExpression> -> OpenParenthesis <SpaceStar> <Expression> <SpaceStar> CloseParenthesis
<PaddedExpression> -> <SpaceStar> <Expression> <WhitespaceStar>
<Expression> -> <IfExpression> | <ElvisExpression>
<SimpleExpression> -> <Number> | <BooleanLiteral> | <StringLiteral> |
                      OpenParenthesis <PaddedExpression> <SpaceStar> CloseParenthesis | SimpleName
<BooleanLiteral> -> KeywordFalse | KeywordTrue
<Number> -> DecimalLiteral <NumberRest>
<NumberRest> -> Dot <DecimalLiteralOrMemberAccess> | ε
<DecimalLiteralOrMemberAccess> -> DecimalLiteral | <MemberAccess>
<StringLiteral> -> Apostrophes <AnythingEndsWithApostrophes>
<AnythingButApostrophes> -> WhiteSpace | SemiColon | BreakLine | Colon | Dot | Comma | Underscore |
                            Percentage | Backslash | Star | Minus | Plus | ExclamationMark | QuestionMark | And | Or |
                            Assignment | OpenBraces | CloseBraces | OpenParenthesis | CloseParenthesis | OpenBrokets |
                            CloseBrokets | DecimalLiteral | SimpleName |
                            KeywordVal | KeywordVar | KeywordFun | KeywordClass | KeywordTrue | KeywordFalse |
                            KeywordIf | KeywordElse | KeywordWhile | KeywordWhen |
                            Unknown
<AnythingEndsWithApostrophes> -> <AnythingButApostrophes> <AnythingEndsWithApostrophes> | Apostrophes
<IfExpression> -> KeywordIf <SpaceStar> OpenParenthesis <PaddedExpression> <SpaceStar> CloseParenthesis <SpaceStar>
                  <BlockOrStatement> <SpaceStar> <IfExpressionRest>
<IfExpressionRest> -> KeywordElse <SpaceStar> <BlockOrStatement> | ε


// Class
<ClassDeclaration> -> KeywordClass <SpacePlus> SimpleName <SpaceStar> OpenBraces
                      <SpaceStar> <MemberDeclarationStar> <SEMIOptional> CloseBraces
<MemberDeclarationStar> -> <MemberDeclaration> <WhitespaceStar> <MemberDeclarationStarRest> | ε
<MemberDeclarationStarRest> -> <SEMI> <MemberDeclarationStar> | ε

<MemberDeclaration> -> <PrivateOrPublic> <SpacePlus> <MemberDeclarationRest>
<MemberDeclarationRest> -> <ValDeclaration> | <FunctionDeclaration>

<PrivateOrPublic> -> KeywordPrivate | KeywordPublic


// Binary Operators
<ElvisExpression> -> <DisjunctionExpression> <WhitespaceStar> <ElvisExpressionRest>
<ElvisExpressionRest> -> Colon QuestionMark <SpaceStar> <ElvisExpression> | ε

<DisjunctionExpression> -> <ConjunctionExpression> <WhitespaceStar> <DisjunctionExpressionRest>
<DisjunctionExpressionRest> -> Or Or <SpaceStar> <DisjunctionExpression> | ε

<ConjunctionExpression> -> <EqualityExpression> <WhitespaceStar> <ConjunctionExpressionRest>
<ConjunctionExpressionRest> -> And And <SpaceStar> <ConjunctionExpression> | ε

<EqualityExpression> -> <ComparisonExpression> <WhitespaceStar> <EqualityExpressionRest>
<EqualityExpressionRest> -> Assignment Assignment <SpaceStar> <EqualityExpression> | ε

<ComparisonExpression> -> <AdditiveExpression> <WhitespaceStar> <ComparisonExpressionRest>
<ComparisonExpressionRest> -> Assignment Assignment <SpaceStar> <ComparisonExpression> | ε

<AdditiveOperator> -> Plus | Minus
<AdditiveExpression> -> <MultiplicativeExpression> <WhitespaceStar> <AdditiveExpressionRest>
<AdditiveExpressionRest> -> <AdditiveOperator> <SpaceStar> <AdditiveExpression> | ε

<MultiplicativeOperator> -> Star | Backslash | Percentage
<MultiplicativeExpression> -> <Invocation> <WhitespaceStar> <MultiplicativeExpressionRest>
<MultiplicativeExpressionRest> -> <MultiplicativeOperator> <SpaceStar> <MultiplicativeExpression> | ε

<Invocation> -> <SimpleExpression> <WhitespaceStar> <InvocationRest> |
                <ParenthesizedExpression> <WhitespaceStar> <InvocationRest>
// TODO: null-aware invocation (remember to solve the clash with elvis operator)
<InvocationRest> -> Dot <SpaceStar> <MemberAccess> | ε
<MemberAccess> -> SimpleName <MaybeFunctionArguments>
<MaybeFunctionArguments> -> <FunctionArguments> | ε
<FunctionArguments> -> OpenParenthesis <ArgumentStar> CloseParenthesis
<ArgumentStar> -> <Expression> <ArgumentStarRest> | ε
<ArgumentStarRest> -> Comma <ArgumentStar> | ε

// Function Declaration
<FunctionDeclaration> -> KeywordFun <SpacePlus> SimpleName <SpaceStar> <TypeParameters> <SpaceStar>
                         <FunctionParameters> <SpaceStar> <ReturnTypeOptional> <SpaceStar> <Block>
<ReturnTypeOptional> -> Colon SimpleName | ε
<TypeParameters> -> OpenBrokets <SpaceStar> SimpleName <SpaceStar> CloseBrokets | ε
<FunctionParameters> -> OpenParenthesis <FunctionParameterStar> CloseParenthesis
<FunctionParameterStar> -> <FunctionParameter> <FunctionParameterStarRest> | ε
<FunctionParameterStarRest> -> Comma <FunctionParameterStar> | ε
<FunctionParameter> -> SimpleName Colon SimpleName


// General Stuff
<WhiteSpaceOrBreakLine> -> WhiteSpace | BreakLine

<SpaceStar> -> <WhiteSpaceOrBreakLine> <SpaceStar> | ε
<SpacePlus> -> <WhiteSpaceOrBreakLine> <SpaceStar>

<WhitespaceStar> -> WhiteSpace <WhitespaceStar> | ε
<WhitespacePlus> -> WhiteSpace <WhitespaceStar>

<UnderscoreStar> -> Underscore <UnderscoreStar> | ε
<UnderscorePlus> -> Underscore <UnderscoreStar>

<SEMI> -> <WhitespacePlus> <SEMIRest> <SpaceStar> | <SEMIRest> <SpaceStar>
<SEMIRest> -> BreakLine <SpaceStar> <SemiColonOptional> | SemiColon

<SEMIOptional> -> <SEMI> | ε

<BreakLineOptional> -> BreakLine | ε
<SemiColonOptional> -> SemiColon | ε