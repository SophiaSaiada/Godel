@Tokens
    WhiteSpace, SemiColon, BreakLine, Colon, Dot, Comma, Apostrophes, Underscore,
    Percentage, Backslash, Star, Minus, Plus, ExclamationMark, And, Or,
    Assignment, OpenBraces, CloseBraces, OpenParenthesis, CloseParenthesis, OpenBrokets, CloseBrokets,
    DecimalLiteral, SimpleName,

    KeywordVal, KeywordVar, KeywordFun, KeywordClass, KeywordTrue, KeywordFalse,
    KeywordIf, KeywordElse, KeywordWhile, KeywordPrivate, KeywordPublic, KeywordWhen,

    Unknown

@Rules
<Statements> -> <PaddedStatement> <StatementsRest> | ε
<StatementsRest> -> <SEMI> <Statements> | ε

<PaddedStatement> -> <SpacePlus> <Statement> <SpaceStar> | <Statement> <SpaceStar>
<Statement> -> <Expression> | <Declaration>
<Block> -> OpenBraces <Statements> CloseBraces
<BlockOrStatement> -> <Block> | <Statement>

<Declaration> -> <ValDeclaration> | <ClassDeclaration>
<ValDeclaration> -> KeywordVal <SpacePlus> <UnderscoreStar> SimpleName <SpaceStar> <ValDeclarationRest>
<ValDeclarationRest> -> Colon <SpaceStar> SimpleName <SpaceStar> Assignment <PaddedExpression> |
                        Assignment <PaddedExpression>

<PaddedExpression> -> <SpaceStar> <Expression> <SpaceStar>
<Expression> -> <IfExpression> | <DisjunctionExpression>
<SimpleExpression> -> <Number> | <BooleanLiteral> | <StringLiteral> |
                      OpenParenthesis <PaddedExpression> CloseParenthesis | SimpleName
<BooleanLiteral> -> KeywordFalse | KeywordTrue
<Number> -> DecimalLiteral <NumberRest>
<NumberRest> -> Dot DecimalLiteral | ε
<StringLiteral> -> Apostrophes <AnythingEndsWithApostrophes>
<AnythingButApostrophes> -> WhiteSpace | SemiColon | BreakLine | Colon | Dot | Comma | Underscore |
                            Percentage | Backslash | Star | Minus | Plus | ExclamationMark | And | Or | Assignment |
                            OpenBraces | CloseBraces | OpenParenthesis | CloseParenthesis | OpenBrokets | CloseBrokets |
                            DecimalLiteral | SimpleName |
                            KeywordVal | KeywordVar | KeywordFun | KeywordClass | KeywordTrue | KeywordFalse |
                            KeywordIf | KeywordElse | KeywordWhile | KeywordWhen |
                            Unknown
<AnythingEndsWithApostrophes> -> <AnythingButApostrophes> <AnythingEndsWithApostrophes> | Apostrophes
<IfExpression> -> KeywordIf <SpaceStar> OpenParenthesis <PaddedExpression> CloseParenthesis <SpaceStar>
                  <BlockOrStatement> <SpaceStar> <IfExpressionRest>
<IfExpressionRest> -> KeywordElse <SpaceStar> <BlockOrStatement> | ε


// Class
<ClassDeclaration> -> KeywordClass <SpacePlus> SimpleName <SpaceStar> OpenBraces
                      <SpaceStar> <MemberDeclarationStar> CloseBraces
<MemberDeclarationStar> -> <MemberDeclaration> <WhitespaceStar> <MemberDeclarationStarRest> | ε
<MemberDeclarationStarRest> -> <SEMI> <SpaceStar> <MemberDeclarationStar> | ε

<MemberDeclaration> -> <PrivateOrPublic> <SpacePlus> <MemberDeclarationRest>
<MemberDeclarationRest> -> <ValDeclaration> | <FunctionDeclaration>

<PrivateOrPublic> -> KeywordPrivate | KeywordPublic


// Binary Operators
<DisjunctionExpression> -> <ConjunctionExpression> <SpaceStar> <DisjunctionExpressionRest>
<DisjunctionExpressionRest> -> Or Or <SpaceStar> <ConjunctionExpression> | ε

<ConjunctionExpression> -> <EqualityExpression> <SpaceStar> <ConjunctionExpressionRest>
<ConjunctionExpressionRest> -> And And <SpaceStar> <EqualityExpression> | ε

<EqualityExpression> -> <AdditiveExpression> <SpaceStar> <EqualityExpressionRest>
<EqualityExpressionRest> -> Assignment Assignment <SpaceStar> <EqualityExpression> | ε

<AdditiveOperator> -> Plus | Minus
<AdditiveExpression> -> <MultiplicativeExpression> <SpaceStar> <AdditiveExpressionRest>
<AdditiveExpressionRest> -> <AdditiveOperator> <SpaceStar> <AdditiveExpression> | ε

<MultiplicativeOperator> -> Star | Backslash | Percentage
<MultiplicativeExpression> -> <SimpleExpression> <SpaceStar> <MultiplicativeExpressionRest>
<MultiplicativeExpressionRest> -> <MultiplicativeOperator> <SpaceStar> <MultiplicativeExpression> | ε


// Function Declaration
<FunctionDeclaration> -> KeywordFun <SpacePlus> SimpleName <SpaceStar> <TypeParameters> <SpaceStar>
                         <FunctionParameters> <SpaceStar> <ReturnTypeOptional> <SpaceStar> <Block>
<ReturnTypeOptional> -> Colon SimpleName | ε
<TypeParameters> -> OpenBrokets <SpaceStar> SimpleName <SpaceStar> CloseBrokets | ε
<FunctionParameters> -> OpenParenthesis <FunctionParameterStar> CloseParenthesis
<FunctionParameterStar> -> <FunctionParameter> <FunctionParameterStarRest> | ε
<FunctionParameterStarRest> -> Comma <FunctionParameterStar> | ε
<FunctionParameter> -> SimpleName Colon SimpleName


// General Stuff
<WhiteSpaceOrBreakLine> -> WhiteSpace | BreakLine

<SpaceStar> -> <WhiteSpaceOrBreakLine> <SpaceStar> | ε
<SpacePlus> -> <WhiteSpaceOrBreakLine> <SpaceStar>

<WhitespaceStar> -> WhiteSpace <WhitespaceStar> | ε

<UnderscoreStar> -> Underscore <UnderscoreStar> | ε
<UnderscorePlus> -> Underscore <UnderscoreStar>

<SEMI> -> SemiColon | BreakLine
<SEMIOptional> -> <SEMI> | ε