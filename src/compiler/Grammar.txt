@Tokens
    SemiColon, BreakLine, WhiteSpace, Dot, Comma, Colon, Assignment, Apostrophes, OpenParenthesis, CloseParenthesis, Underscore,
    OpenBraces, CloseBraces,OpenBrokets, CloseBrokets, DecimalLiteral, KeywordFalse, KeywordTrue, KeywordVal, KeywordIf, KeywordElse, KeywordFun, SimpleName,
    Unknown

@Rules
<Statements> -> <PaddedStatement> <StatementsRest>
<StatementsRest> -> <SEMI> <Statements> | ε

<PaddedStatement> -> <SpacePlus> <Statement> <SpaceStar> | <Statement> <SpaceStar>
<Statement> -> <Expression> | <Declaration>
<Block> -> OpenBraces <Statements> CloseBraces
<BlockOrStatement> -> <Block> | <Statement>

<Declaration> -> <ValDeclaration>
<ValDeclaration> -> KeywordVal <SpacePlus> <UnderscoreStar> SimpleName <SpaceStar> <ValDeclarationRest>
<ValDeclarationRest> -> Colon <SpaceStar> SimpleName <SpaceStar> Assignment <PaddedExpression> |
                        Assignment <PaddedExpression>

<PaddedExpression> -> <SpaceStar> <Expression> <SpaceStar>
<Expression> -> <Number> | <BooleanLiteral> | <StringLiteral> | <IfExpression> |
                OpenParenthesis <PaddedExpression> CloseParenthesis | SimpleName
<BooleanLiteral> -> KeywordFalse | KeywordTrue
<Number> -> DecimalLiteral <NumberRest>
<NumberRest> -> Dot DecimalLiteral | ε
<StringLiteral> -> Apostrophes <AnythingEndsWithApostrophes>
<AnythingButApostrophes> -> SemiColon | BreakLine | WhiteSpace | Dot | Colon | Assignment | OpenParenthesis |
                            CloseParenthesis | OpenBraces | CloseBraces | DecimalLiteral | KeywordFalse | KeywordTrue | KeywordVal | KeywordIf | KeywordElse | SimpleName | Unknown
<AnythingEndsWithApostrophes> -> <AnythingButApostrophes> <AnythingEndsWithApostrophes> | Apostrophes
<IfExpression> -> KeywordIf <SpaceStar> OpenParenthesis <PaddedExpression> CloseParenthesis <SpaceStar>
                  <BlockOrStatement> <SpaceStar> <IfExpressionRest>
<IfExpressionRest> -> KeywordElse <SpaceStar> <BlockOrStatement> | ε
//Function Declaration
<FunctionDeclaration> -> KeywordFun <SpacePlus> SimpleName <SpaceStar> <FunctionGenericType> <SpaceStar> <FunctionParameters> <FunctionReturnType> <Block>
<FunctionReturnType> -> <SpaceStar> Colon <SpaceStar> SimpleName <SpaceStar> | ε
<FunctionGenericType> -> OpenBrokets <SpaceStar> SimpleName <SpaceStar> CloseBrokets | ε
<FunctionParameters> -> OpenParenthesis <SpaceStar> <ParameterStar> <SpaceStar> CloseParenthesis
<ParameterStar> -> <ParameterPlus> | ε
<ParameterPlus> -> <Parameter> <ParameterPlusRest>
<ParameterPlusRest> -> Comma <ParameterPlus> | ε
<Parameter> -> <SpaceStar> SimpleName <SpaceStar> Colon <SpaceStar> SimpleName <SpaceStar>
<SpacePlus> -> <WhiteSpaceOrBreakLine> <SpacePlusRest>
<SpacePlusRest> -> <SpacePlus> | ε
<SpaceStar> -> <SpacePlus> | ε
<WhiteSpaceOrBreakLine> -> WhiteSpace | BreakLine

<UnderscoreStar> -> <UnderscorePlus> | ε
<UnderscorePlus> -> Underscore <UnderscorePlusRest>
<UnderscorePlusRest> -> <UnderscorePlus> | ε

<SEMI> -> SemiColon | BreakLine