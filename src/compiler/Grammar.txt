@Tokens
    WhiteSpace, SemiColon, BreakLine, Colon, Dot, Comma, Apostrophes, Underscore,
    Percentage, Backslash, Star, Minus, Plus, ExclamationMark, QuestionMark, And, Or,
    Assignment, OpenBraces, CloseBraces, OpenParenthesis, CloseParenthesis, OpenBrokets, CloseBrokets,
    DecimalLiteral, SimpleName, QuestionedDot, Elvis,

    KeywordVal, KeywordVar, KeywordFun, KeywordClass, KeywordTrue, KeywordFalse,
    KeywordIf, KeywordElse, KeywordWhile, KeywordPrivate, KeywordPublic, KeywordWhen,

    Unknown

@Rules
<Program> -> <SpaceStar> <Statements> <SpaceStar>
<Statements> -> <Statement> <WhitespaceStar> <StatementsRest> | ε
<StatementsRest> -> <SEMI> <Statements> | ε

<Statement> -> <Expression> | <Declaration>
<Block> -> OpenBraces <Program> CloseBraces
<BlockOrStatement> -> <Block> | <Statement>

<Declaration> -> <ValDeclaration> | <ClassDeclaration> | <FunctionDeclaration>
<ValDeclaration> -> KeywordVal <SpacePlus> <UnderscoreStar> SimpleName <SpaceStar> <ValDeclarationRest>
<ValDeclarationRest> -> Colon <SpaceStar> <Type> <SpaceStar> Assignment <PaddedExpression> |
                        Assignment <PaddedExpression>

<Type> -> SimpleName <TypeParameters> <QuestionMarkOptional>
<TypeParameters> -> OpenBrokets <SpaceStar> <TypeParametersNamesPlus> <SpaceStar> CloseBrokets | ε
<TypeParametersNamesPlus> -> SimpleName <SpaceStar> <TypeParametersInheritanceOptional> <TypeParametersNamesPlusRest>
<TypeParametersInheritanceOptional> -> Colon <SpaceStar> <Type> | ε
<TypeParametersNamesPlusRest> -> Comma <SpaceStar> <TypeParametersNamesPlus> | ε
<QuestionMarkOptional> -> QuestionMark | ε

<ParenthesizedExpression> -> OpenParenthesis <SpaceStar> <Expression> <SpaceStar> CloseParenthesis
<PaddedExpression> -> <SpaceStar> <Expression> <WhitespaceStar>
<Expression> -> <IfExpression> | <ElseExpression> | <ElvisExpression>
<SimpleExpression> -> <Number> | <BooleanLiteral> | <StringLiteral> | SimpleName
<BooleanLiteral> -> KeywordFalse | KeywordTrue
<Number> -> DecimalLiteral <NumberRest>
<NumberRest> -> Dot <DecimalLiteralOrMemberAccess> | ε
<DecimalLiteralOrMemberAccess> -> DecimalLiteral | <MemberAccessWithoutFirstDot>
<MemberAccessWithoutFirstDot> -> <SpaceStar> SimpleName

<StringLiteral> -> Apostrophes <AnythingEndsWithApostrophes>
<AnythingButApostrophes> -> WhiteSpace | SemiColon | BreakLine | Colon | Dot | Comma | Underscore |
                            Percentage | Backslash | Star | Minus | Plus | ExclamationMark | QuestionMark | And | Or |
                            Assignment | OpenBraces | CloseBraces | OpenParenthesis | CloseParenthesis | OpenBrokets |
                            CloseBrokets | DecimalLiteral | SimpleName | QuestionedDot | Elvis |
                            KeywordVal | KeywordVar | KeywordFun | KeywordClass | KeywordTrue | KeywordFalse |
                            KeywordIf | KeywordElse | KeywordWhile | KeywordWhen |
                            Unknown
<AnythingEndsWithApostrophes> -> <AnythingButApostrophes> <AnythingEndsWithApostrophes> | Apostrophes
<IfExpression> -> KeywordIf <SpaceStar> <ParenthesizedExpression> <SpaceStar>
                  <BlockOrStatement> <WhitespaceStar> <IfExpressionRest>
<IfExpressionRest> -> <ElseExpression> | ε
<ElseExpression> -> KeywordElse <SpaceStar> <BlockOrStatement>

// Class
<ClassDeclaration> -> KeywordClass <SpacePlus> SimpleName <SpaceStar> OpenBraces
                      <SpaceStar> <MemberDeclarationStar> <SEMIOptional> CloseBraces
<MemberDeclarationStar> -> <MemberDeclaration> <WhitespaceStar> <MemberDeclarationStarRest> | ε
<MemberDeclarationStarRest> -> <SEMI> <MemberDeclarationStar> | ε

<MemberDeclaration> -> <PrivateOrPublic> <SpacePlus> <MemberDeclarationRest>
<MemberDeclarationRest> -> <ValDeclaration> | <FunctionDeclaration>

<PrivateOrPublic> -> KeywordPrivate | KeywordPublic


// Binary Operators
<ElvisExpression> -> <InfixExpression> <WhitespaceStar> <ElvisExpressionRest>
<ElvisExpressionRest> -> Elvis <SpaceStar> <ElvisExpression> | ε

<InfixExpression> -> <DisjunctionExpression> <WhitespaceStar> <InfixExpressionRest>
<InfixExpressionRest> -> SimpleName <SpaceStar> <InfixExpression> | ε

<DisjunctionExpression> -> <ConjunctionExpression> <WhitespaceStar> <DisjunctionExpressionRest>
<DisjunctionExpressionRest> -> Or Or <SpaceStar> <DisjunctionExpression> | ε

<ConjunctionExpression> -> <EqualityExpression> <WhitespaceStar> <ConjunctionExpressionRest>
<ConjunctionExpressionRest> -> And And <SpaceStar> <ConjunctionExpression> | ε

<EqualityOperator> -> Assignment Assignment | ExclamationMark Assignment
<EqualityExpression> -> <ComparisonExpression> <WhitespaceStar> <EqualityExpressionRest>
<EqualityExpressionRest> -> <EqualityOperator> <SpaceStar> <EqualityExpression> | ε

<ComparisonOperator> -> OpenBrokets <AssignmentOptional> | CloseBrokets <AssignmentOptional>
<ComparisonExpression> -> <AdditiveExpression> <WhitespaceStar> <ComparisonExpressionRest>
<ComparisonExpressionRest> -> <ComparisonOperator> <SpaceStar> <ComparisonExpression> | ε

<AdditiveOperator> -> Plus | Minus
<AdditiveExpression> -> <MultiplicativeExpression> <WhitespaceStar> <AdditiveExpressionRest>
<AdditiveExpressionRest> -> <AdditiveOperator> <SpaceStar> <AdditiveExpression> | ε

<MultiplicativeOperator> -> Star | Backslash | Percentage
<MultiplicativeExpression> -> <MemberAccess> <WhitespaceStar> <MultiplicativeExpressionRest>
<MultiplicativeExpressionRest> -> <MultiplicativeOperator> <SpaceStar> <MultiplicativeExpression> | ε

<DotOrQuestionedDot> -> Dot | QuestionedDot
<MemberAccess> -> <Invocation> <WhitespaceStar> <MemberAccessRest>
<MemberAccessRest> -> <DotOrQuestionedDot> <SpaceStar> <MemberAccess> | ε

<Invocation> -> <SimpleOrParenthesizedExpression> <WhitespaceStar> <InvocationArgumentsStar>
<InvocationArgumentsStar> -> <InvocationArguments> <WhitespaceStar> <InvocationArgumentsStar> | ε
<InvocationArguments> -> OpenParenthesis <SpaceStar> <ArgumentStar> CloseParenthesis
<ArgumentStar> -> <Expression> <SpaceStar> <ArgumentStarRest> | ε
<ArgumentStarRest> -> Comma <SpaceStar> <ArgumentStar> | ε

<SimpleOrParenthesizedExpression> -> <SimpleExpression> | <ParenthesizedExpression>


// Function Declaration
<FunctionDeclaration> -> KeywordFun <SpacePlus> SimpleName <SpaceStar> <TypeParameters> <SpaceStar>
                         <FunctionParameters> <SpaceStar> <ReturnTypeOptional> <SpaceStar> <Block>
<ReturnTypeOptional> -> Colon <SpaceStar> SimpleName | ε
<FunctionParameters> -> OpenParenthesis <FunctionParameterStar> CloseParenthesis
<FunctionParameterStar> -> <FunctionParameter> <FunctionParameterStarRest> | ε
<FunctionParameterStarRest> -> Comma <FunctionParameterStar> | ε
<FunctionParameter> -> SimpleName Colon SimpleName


// General Stuff
<WhiteSpaceOrBreakLine> -> WhiteSpace | BreakLine

<SpaceStar> -> <WhiteSpaceOrBreakLine> <SpaceStar> | ε
<SpacePlus> -> <WhiteSpaceOrBreakLine> <SpaceStar>

<WhitespaceStar> -> WhiteSpace <WhitespaceStar> | ε
<WhitespacePlus> -> WhiteSpace <WhitespaceStar>

<UnderscoreStar> -> Underscore <UnderscoreStar> | ε
<UnderscorePlus> -> Underscore <UnderscoreStar>

<SEMI> -> <WhitespacePlus> <SEMIRest> <SpaceStar> | <SEMIRest> <SpaceStar>
<SEMIRest> -> BreakLine <SpaceStar> <SemiColonOptional> | SemiColon

<SEMIOptional> -> <SEMI> | ε

<BreakLineOptional> -> BreakLine | ε
<SemiColonOptional> -> SemiColon | ε
<AssignmentOptional> -> Assignment | ε