@Tokens
    WhiteSpace, SemiColon, BreakLine, Colon, Dot, Comma, Apostrophes, Backtick,
    Percentage, Backslash, Star, Minus, Plus, Division, ExclamationMark, QuestionMark, Ampersand, SingleOr,
    Keyword, Assignment, QuestionedDot, Hash,

    OpenBraces, CloseBraces, OpenParenthesis, CloseParenthesis, OpenBrokets, CloseBrokets,
    DecimalLiteral, SimpleName,

    Elvis, Or, And, Equal, NotEqual, GreaterThanEqual, LessThanEqual, NullAwareDot, RightArrow,

    KeywordVal, KeywordVar, KeywordFun, KeywordClass, KeywordTrue, KeywordFalse,
    KeywordIf, KeywordElse, KeywordWhile, KeywordPrivate, KeywordPublic, KeywordWhen, KeywordReturn,

    Unknown

@Rules
<Program> -> <SpaceStar> <Statements> <SpaceStar>
<Statements> -> <Statement> | <Statement> <WhitespacePlus> <SEMI> <Statements> | <Statement> <SEMI> <Statements> | ε

<Statement> -> <Expression> | <Declaration>

<Declaration> -> <ValDeclaration> | <ClassDeclaration> | <FunctionDeclaration>

<ValDeclaration> -> KeywordVal <SpacePlus> SimpleName <SpaceStar> <ValDeclarationRest>
<ValDeclarationRest> -> Colon <SpaceStar> <Type> <SpaceStar> Assignment <PaddedExpression> |
                        Assignment <PaddedExpression>

<Type> -> SimpleName <TypeArgumentsOptional> <QuestionMarkOptional> |
          OpenParenthesis <SpaceStar> <TypeStar> <SpaceStar> CloseParenthesis <WhitespaceStar> <FunctionalOrNullableType>
<FunctionalOrNullableType> -> RightArrow <SpaceStar> <Type> | QuestionMark | ε
<TypeStar> -> <Type> <SpaceStar> <TypeStarRest> | ε
<TypeStarRest> -> Comma <SpaceStar> <TypeStar> | ε

<TypeArgumentsOptional> -> <TypeArguments> | ε
<TypeArguments> -> OpenBrokets <SpaceStar> <TypeArgumentsContent> CloseBrokets
<TypeArgumentsContent> -> <Type> <SpaceStar> <TypeNamedArgumentsOptional> <SpaceStar> <TypeArgumentsContentRest>
<TypeNamedArgumentsOptional> -> Assignment <SpaceStar> <Type> | ε
<TypeArgumentsContentRest> -> Comma <SpaceStar> <TypeArgumentsContent> | ε

<TypeParameters> -> OpenBrokets <SpaceStar> <TypeParametersNamesPlus> <SpaceStar> CloseBrokets | ε
<TypeParametersNamesPlus> -> SimpleName <SpaceStar> <TypeParametersInheritanceOptional> <TypeParametersNamesPlusRest>
<TypeParametersInheritanceOptional> -> Colon <SpaceStar> <Type> | ε
<TypeParametersNamesPlusRest> -> Comma <SpaceStar> <TypeParametersNamesPlus> | ε
<QuestionMarkOptional> -> QuestionMark | ε

<ParenthesizedExpression> -> OpenParenthesis <SpaceStar> <Expression> <SpaceStar> CloseParenthesis
<PaddedExpression> -> <SpaceStar> <Expression> <WhitespaceStar>
<Expression> -> <IfExpression> | <ElseExpression> | <ElvisExpression>
<SimpleExpression> -> <Number> | <BooleanLiteral> | <StringLiteral> | SimpleName | <Lambda>
<BooleanLiteral> -> KeywordFalse | KeywordTrue
<Number> -> DecimalLiteral Dot DecimalLiteral | DecimalLiteral Dot <MemberAccessWithoutFirstDot> |
            DecimalLiteral
<MemberAccessWithoutFirstDot> -> <SpaceStar> SimpleName

<StringLiteral> -> Apostrophes <AnythingEndsWithApostrophes>
<AnythingEndsWithApostrophes> -> <AnythingBut@Apostrophes> <AnythingEndsWithApostrophes> | Apostrophes
<IfExpression> -> KeywordIf <SpaceStar> <ParenthesizedExpression> <SpaceStar> <StatementOrBlock> <WhitespaceStar> |
                  KeywordIf <SpaceStar> <ParenthesizedExpression> <SpaceStar> <StatementOrBlock> <WhitespaceStar> <ElseExpression>
<ElseExpression> -> KeywordElse <SpaceStar> <StatementOrBlock>

// Class
<ClassDeclaration> -> KeywordClass <SpacePlus> SimpleName <SpaceStar> <TypeParameters> <SpaceStar>
                      OpenParenthesis <SpaceStar> <ClassPropertyStar> CloseParenthesis <SpaceStar>
                      OpenBraces <SpaceStar> <MemberDeclarationStar> <SEMIOptional> CloseBraces
<ClassPropertyStar> -> <ClassProperty> <SpaceStar> | <ClassProperty> Comma <SpaceStar> <ClassPropertyStar> | ε
<ClassProperty> -> <PrivateOrPublic> <SpacePlus> KeywordVal <SpacePlus> SimpleName <SpaceStar> Colon <SpaceStar> <Type> <SpaceStar>
<MemberDeclarationStar> -> <MemberDeclaration> <WhitespaceStar> <MemberDeclarationStarRest> | ε
<MemberDeclarationStarRest> -> <SEMI> <MemberDeclarationStar> | ε

<MemberDeclaration> -> <PrivateOrPublic> <SpacePlus> <MemberDeclarationRest>
<MemberDeclarationRest> -> <ValDeclaration> | <FunctionDeclaration>

<PrivateOrPublic> -> KeywordPrivate | KeywordPublic


// Binary Operators
<ElvisExpression> -> <ReturnExpression> <WhitespaceStar> Elvis <SpaceStar> <ElvisExpression> |
                     <ReturnExpression> <WhitespaceStar>

<ReturnExpression> -> KeywordReturn <SpacePlus> <ReturnExpression> | <InfixExpression>

<InfixExpression> -> <DisjunctionExpression> <WhitespaceStar> SimpleName <SpaceStar> <InfixExpression> |
                     <DisjunctionExpression> <WhitespaceStar>

<DisjunctionExpression> -> <ConjunctionExpression> <WhitespaceStar> Or <SpaceStar> <DisjunctionExpression> |
                           <ConjunctionExpression> <WhitespaceStar>

<ConjunctionExpression> -> <EqualityExpression> <WhitespaceStar> And <SpaceStar> <ConjunctionExpression> |
                           <EqualityExpression> <WhitespaceStar>

<EqualityOperator> -> Equal | NotEqual
<EqualityExpression> -> <ComparisonExpression> <WhitespaceStar> <EqualityOperator> <SpaceStar> <EqualityExpression> |
                        <ComparisonExpression> <WhitespaceStar>

<ComparisonOperator> -> OpenBrokets | CloseBrokets | LessThanEqual | GreaterThanEqual
<ComparisonExpression> -> <AdditiveExpression> <WhitespaceStar> <ComparisonOperator> <SpaceStar> <ComparisonExpression> |
                          <AdditiveExpression> <WhitespaceStar>

<AdditiveOperator> -> Plus | Minus
<AdditiveExpression> -> <MultiplicativeExpression> <WhitespaceStar> <AdditiveOperator> <SpaceStar> <AdditiveExpression> |
                        <MultiplicativeExpression> <WhitespaceStar>

<MultiplicativeOperator> -> Star | Division | Percentage
<MultiplicativeExpression> -> <MemberAccess> <WhitespaceStar> <MultiplicativeOperator> <SpaceStar> <MultiplicativeExpression> |
                              <MemberAccess> <WhitespaceStar>

<DotOrQuestionedDot> -> Dot | NullAwareDot
<MemberAccess> -> <Invocation> <WhitespaceStar> <DotOrQuestionedDot> <SpaceStar> <MemberAccess> |
                  <Invocation> <WhitespaceStar>

<Invocation> -> <SimpleOrParenthesizedExpression> <WhitespaceStar> <InvocationArgumentsStar>
<InvocationArgumentsStar> -> <TypeArguments> <InvocationArguments> <WhitespaceStar> <InvocationArgumentsStar> |
                             <InvocationArguments> <WhitespaceStar> <InvocationArgumentsStar> | ε
<InvocationArguments> -> OpenParenthesis <SpaceStar> <ArgumentStar> CloseParenthesis
<ArgumentStar> -> <Expression> <SpaceStar> <NamedArgumentPostfixOptional> <ArgumentStarRest> | ε
<NamedArgumentPostfixOptional> -> Assignment <SpaceStar> <Expression> <SpaceStar> | ε
<ArgumentStarRest> -> Comma <SpaceStar> <ArgumentStar> | ε

<SimpleOrParenthesizedExpression> -> <SimpleExpression> | <ParenthesizedExpression>


// Function Declaration
<FunctionDeclaration> -> KeywordFun <SpacePlus> <FunctionName> <SpaceStar> <TypeParameters> <SpaceStar>
                         <FunctionParameters> <SpaceStar> <ReturnTypeOptional> <SpaceStar> <Block>
<FunctionName> -> SimpleName | Backtick <AnythingButBacktickPlus> Backtick
<ReturnTypeOptional> -> Colon <SpaceStar> <Type> | ε
<FunctionParameters> -> OpenParenthesis <SpaceStar> <FunctionParameterStar> <SpaceStar> CloseParenthesis
<FunctionParameterStar> -> <FunctionParameter> <SpaceStar> <FunctionParameterStarRest> | ε
<FunctionParameterStarRest> -> Comma <SpaceStar> <FunctionParameterStar> | ε
<FunctionParameter> -> SimpleName <SpaceStar> Colon <SpaceStar> <Type>

<StatementOrBlock> -> <Block> | <Statement>
<Block> -> OpenBraces <Program> CloseBraces

// Lambda
<Lambda> -> Hash OpenBraces <SpaceStar> <LambdaParametersStar> RightArrow <SpaceStar> <Program> CloseBraces
<LambdaParametersStar> -> SimpleName <SpaceStar> Colon <SpaceStar> <Type> <SpaceStar> <LambdaParametersRest> | ε
<LambdaParametersRest> -> Comma <SpaceStar> <LambdaParametersStar> | ε

// General Stuff
<WhiteSpaceOrBreakLine> -> WhiteSpace | BreakLine

<SpaceStar> -> <WhiteSpaceOrBreakLine> <SpaceStar> | ε
<SpacePlus> -> <WhiteSpaceOrBreakLine> <SpaceStar>

<WhitespaceStar> -> WhiteSpace <WhitespaceStar> | ε
<WhitespacePlus> -> WhiteSpace <WhitespaceStar>

<AnythingButBacktickPlus> -> <AnythingBut@Backtick> | <AnythingBut@Backtick> <AnythingButBacktickPlus>

<SEMI> -> <WhitespacePlus> <SEMIRest> <SpaceStar> | <SEMIRest> <SpaceStar>
<SEMIRest> -> BreakLine <SpaceStar> <SemiColonOptional> | SemiColon

<SEMIOptional> -> <SEMI> | ε

<SemiColonOptional> -> SemiColon | ε
